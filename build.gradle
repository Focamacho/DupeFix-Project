buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {

            url = 'http://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = "3.0.5"
group = "com.focamacho.dupefixproject"
archivesBaseName = "DupeFixProject-1.12.2"

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main {
        ext.refMap = 'mixins.dupefixproject.refmap.json'
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "snapshot_20171003"

    def args = [
            '-Dfml.coreMods.load=com.focamacho.dupefixproject.asm.DupeFixProjectCoreMod',
            '-Dmixin.hotSwap=true',
            '-Dmixin.checks.interfaces=true'
    ]

    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

repositories {
    //mavenCentral()
    maven {
        url = 'http://repo.spongepowered.org/maven'
    }

    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com"
    }
}

dependencies {
    //Tiny Progressions
    compileOnly("curse.maven:tinyprogressions-250850:2721018")

    //The Spice of Life + Dependencies
    compileOnly("curse.maven:thespiceoflife-220811:2571951")
    compileOnly("curse.maven:applecore-224472:2969118")

    //Nether Chest
    compileOnly("curse.maven:netherchest-268888:2655413")

    //Mekanism
    compileOnly("curse.maven:mekanism-268560:2835175")

    //Project Red World + Dependencies
    compileOnly("curse.maven:projectredworld-229049:2745551")
    compileOnly("curse.maven:projectredbase-228702:2745545")
    compileOnly("curse.maven:codechickenlib-242818:2779848")
    compileOnly("curse.maven:forgemultipartcbe-258426:2755790")
    compileOnly("curse.maven:mrtjpcore-229002:2735197")

    //Blood Magic + Dependencies
    compileOnly("curse.maven:bloodmagic-224791:2822288")
    compileOnly("curse.maven:guideapi-228832:2645992")

    //Thaumcraft + Dependencies
    compileOnly("curse.maven:thaumcraft-223628:2629023")
    compileOnly("curse.maven:baubles-227083:2518667")

    //EnderIO + Dependencies
    compileOnly("curse.maven:enderio-64578:2989201")
    compileOnly("curse.maven:endercore-231868:2972849")

    //Tinkers' Construct + Dependencies
    compileOnly("curse.maven:tconstruct-74072:2902483")
    compileOnly("curse.maven:mantle-74924:2713386")

    //Actually Additions
    compileOnly("curse.maven:actuallyaditions-228404:2844115")

    //Industrial Foregoing + Dependencies
    compileOnly("curse.maven:industrialforegoing-266515:2745324")
    compileOnly("curse.maven:teslacorelib-254602:2891841")

    //Extra Utilities
    compileOnly("curse.maven:extrautilities-225561:2678374")

    //Bewitchment
    compileOnly("curse.maven:bewitchment-285439:3044569")

    //Botania
    compileOnly("curse.maven:botania-225643:2846950")

    //The Farlanders
    compileOnly("curse.maven:thefarlanders-336432:2805139")

    //Thaumic Wonders
    compileOnly("curse.maven:thaumicwonders-316704:2787954")

    //Forestry
    compileOnly("curse.maven:forestry-59751:2918418")

    //Thermal Expansion + Dependencies
    compileOnly("curse.maven:thermalexpansion-69163:2926431")
    compileOnly("curse.maven:thermalfoundation-222880:2926428")
    compileOnly("curse.maven:cofhcore-69162:2920433")
    compileOnly("curse.maven:cofhworld-271384:2920434")

    //Mixins
    embed('org.spongepowered:mixin:0.8-SNAPSHOT') {
        transitive = false
    }

    //Utils
    embed group: 'org.json', name: 'json', version: '20200518'
    embed group: 'commons-io', name: 'commons-io', version: '2.7'
}

jar {
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'LICENSE.txt', 'META-INF/MANIFSET.MF', 'META-INF/maven/**', 'META-INF/*.RSA', 'META-INF/*.SF'
    }

    manifest.attributes(
            'FMLCorePluginContainsFMLMod': true,
            'ForceLoadAsMod': true,
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.dupefixproject.json',
            'FMLCorePlugin': 'com.focamacho.dupefixproject.asm.DupeFixProjectCoreMod'
    )
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}